buildscript {
	ext {
		springBootVersion = '3.0.0'
		snippetsDir = file("${rootDir.path}/build/generated-snippets")
	}

	repositories {
		mavenCentral()
	}

	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

plugins {
	id 'org.asciidoctor.convert' version '2.4.0'
}

allprojects {
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'org.asciidoctor.convert'

	group = 'com.example'

	repositories {
		mavenCentral()
		maven { url 'https://repo.spring.io/milestone' }
	}
}

subprojects { project ->
	apply plugin: 'java'

	asciidoctor {
		inputs.dir snippetsDir
		dependsOn test
	}

	bootJar {
		dependsOn asciidoctor
		from("${asciidoctor.outputDir}/html5") {
			into 'static/docs'
		}
	}

	test {
		outputs.dir snippetsDir
		useJUnitPlatform()
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation 'org.springframework.boot:spring-boot-starter-data-redis'
		implementation 'org.springframework.boot:spring-boot-starter-validation'
		implementation 'com.google.guava:guava:31.0.1-jre'

		runtimeOnly 'com.h2database:h2'
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'

		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation 'io.rest-assured:rest-assured'
		testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'
		testImplementation 'org.mockito:mockito-inline'
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}

	if (project.name != 'growing-shop-common') {
		dependencies {
			implementation(this.project(':growing-shop-common')) {
				exclude group: 'org.springframework.boot', module: 'spring-boot-starter-web'
			}
		}
	}
	if (project.name != 'growing-shop-auth') {
		dependencies {
			implementation 'org.springframework.boot:spring-boot-starter-web'
		}
	}
}
